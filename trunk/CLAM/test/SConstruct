#!/usr/bin/python
import sys, os

sys.path.append('../scons/libs')
from buildtools import *
# CLAM unit tests build

clamToolPath="../scons/sconstools"

options = Options('options.cache')

options.Add( PathOption( 'clam_prefix', 'Prefix were CLAM was installed', '') )
options.Add( PathOption( 'install_prefix', 'Prefix where Network editor is going to be installed', '/usr/local') )
if sys.platform == 'win32' :
	options.Add( PathOption( 'cppunit_prefix', 'Prefix were cppunit was installed', '' ))
options.Add( BoolOption( 'release', 'Build CLAM Tests enabling compiler optimizations', 'no') )
options.Add( PathOption( 'test_data_path', 'Path to the TestDataPrefix', '') )

env = Environment( ENV=os.environ, tools=['default', 'clam'], toolpath=[clamToolPath], options=options)

options.Update(env)
options.Save('options.cache', env)
Help(options.GenerateHelpText(env))

env['project'] = 'CLAMTests'

libraries = [
	'clam_core',
	'clam_processing',
	'clam_audioio',
]

env.SConsignFile() # Single signature file

env.EnableClamModules(libs=libraries, path=env['clam_prefix'])
env.Append( CCFLAGS=['-O3', '-fomit-frame-pointer', '-g','-Wall'] )
env.Append( CPPFLAGS = ['-DTEST_DATA_PATH="\\"%s\\""' % env['test_data_path'] ] )

if sys.platform in ['linux2', "darwin"] :
	env.ParseConfig( 'pkg-config --cflags --libs cppunit')
	env.AppendUnique(LIBS=['cppunit']) # TODO: Dapper only hack

if sys.platform == 'win32' :
	env.Append( LIBS=['cppunit_vc7'] )
	env.Append( CPPPATH=[env['cppunit_prefix']+'/include'] )
	env.Append( LIBPATH=[env['cppunit_prefix']+'/lib'] )
	if env['release'] :
		env.Append( CPPFLAGS = ['-DWIN32'] )
		env.Append( CCFLAGS = '/FD /GR /GX /MD /O2 /GL /W3 /Zm1000' )	
		env.Append( LINKFLAGS = ['/LTCG'] )
	else :
		env.Append( CPPFLAGS = ['-DWIN32', '-D_DEBUG'] )
		env.Append( CCFLAGS = '/D /FD /GR /GX /GZ /MDd /Od /W3 /ZI /Zm1000' )
		env.Append( LINKFLAGS = ['/OPT:NOREF', '/OPT:NOICF', '/DEBUG'] )

#TODO: KLUDGE to allow old style and new style CLAM headers
env.Append(CPPPATH=env['clam_prefix']+'/include/CLAM')
env.Append(CPPPATH=env['clam_prefix']+'/include')
env.Append(CPPPATH='include')
env.Append(CPPPATH=os.path.join('..','UnitTests', 'CommonHelpers'))
env.Append(CPPPATH=os.path.join('..','UnitTests'))
env.Append(CPPPATH=os.path.join('..','FunctionalTests','CommonHelpers'))

Export( 'env' )
SConscript('UnitTests/SConscript')
SConscript('FunctionalTests/SConscript')

Alias( 'all', ['run_unit_tests','run_functional_tests'] )

