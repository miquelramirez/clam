#!/usr/bin/env python
import os, glob, sys

moduleName='expression'
libraryName='clam_'+moduleName
version = "1.4.0"
description = "Experimental expression compiler for CLAM framework"
url = 'http://clam-project.org'
clamDependencies = [
	'clam_core',
	'clam_processing',
	]
otherDependencies = [
	]

options = Variables('options.cache', ARGUMENTS)
options.Add(PathVariable('clam_prefix', 'The prefix where CLAM was installed', '/usr/local'))
options.Add(PathVariable('prefix', 'Installation prefix (normally /usr, by default this is clam_prefix)', "", validator=PathVariable.PathAccept))
options.Add(PathVariable('prefix_for_packaging', 'Install location when packaging. Use "." for same as prefix.', '.'))
options.Add(BoolVariable('verbose', 'Display the full command line', 'no') )
options.Add(PathVariable('sandbox_path', 'Path where third party libraries were installed (in windows)', "", validator=PathVariable.PathAccept))
options.Add(BoolVariable('crossmingw', 'Using MinGW crosscompiler mode', 'no') )
toolChain = 'default'
if sys.platform == "win32": toolChain = 'mingw'
env = Environment(ENV=os.environ, tools=[toolChain], options=options)
options.Save('options.cache', env)
Help(options.GenerateHelpText(env))
env.SConsignFile() # Single signature file

CLAMInstallDir = env['clam_prefix']
if not env['prefix'] : env['prefix'] = env['clam_prefix']
#TODO: CHECK THIS
if env['prefix_for_packaging'] == '.' : env['prefix_for_packaging'] = env['prefix']

clam_sconstoolspath = os.path.join(CLAMInstallDir,'share','clam','sconstools')
if not os.access(os.path.join(clam_sconstoolspath,"clam.py"),os.R_OK):
	raise Exception("CLAM not installed at '%s'. Use clam_prefix option."%CLAMInstallDir)
if env['crossmingw'] :
	env.Tool('crossmingw', toolpath=[clam_sconstoolspath])
env.Tool('clam', toolpath=[clam_sconstoolspath])
env.EnableClamModules(clamDependencies, CLAMInstallDir)

# Sources and headers
sourcePaths = env.recursiveDirs("mini")
sources = env.scanFiles('*.cxx', sourcePaths) + env.scanFiles('*.cpp', sourcePaths)
sources = dict.fromkeys(sources).keys()
headers = env.scanFiles('*.hxx', sourcePaths) + env.scanFiles('*.hpp', sourcePaths)
env.AppendUnique(CPPPATH=sourcePaths)
pkgconfig = "pkg-config"
if sys.platform == "win32" or env['crossmingw'] :
	pkgconfig = "PKG_CONFIG_PATH=%s %s"%(
		os.path.join(env['sandbox_path'],'local','lib','pkgconfig'),
		os.path.join(env['sandbox_path'],'local','bin','pkg-config.exe'))
	env.AppendUnique(LIBS=['pthread'])
	env.ParseConfig('PKG_CONFIG_PATH=%s pkg-config --cflags --libs sndfile'%os.path.join(env['sandbox_path'],'local','lib','pkgconfig'))

env.Append( CCFLAGS=['-O3','-Wall','-g'] )

sources = [s for s in sources if "main.cpp" not in s]
sources += ["ControlExpression.cxx"]
headers += ["ControlExpression.hxx"]

install, default = env.ClamModule(
	moduleName,
	version,
	description = description,
	url = url,
	sources = sources,
	headers = headers,
	clamDependencies = clamDependencies,
	otherDependencies = otherDependencies,
	)

env.Alias('install', install)
env.Default(default)

