#! /usr/bin/python

import os, glob, sys

bundlename = "clam_lv2_example.lv2"

libraryName='clam_lv2_example'

options = Variables('options.cache', ARGUMENTS)
options.Add(PathVariable('clam_prefix', 'The prefix where CLAM was installed', ''))
options.Add(BoolVariable('verbose', 'Display the full command line instead a short command description', 'no') )
#options.Add(PathVariable('prefix', 'Installation prefix (normally /usr, by default this is clam_prefix)', '', validator=PathVariable.PathAccept))
options.Add(BoolVariable('crossmingw', 'Using MinGW crosscompiler mode', 'no') )

bundlePath=os.path.expanduser(os.path.join("~/.lv2",bundlename))

toolChain = 'default'
if sys.platform == 'win32': toolChain = 'mingw'
env = Environment(ENV=os.environ, tools=[toolChain], options=options)
options.Save('options.cache', env) # do not save options in NetworkEditor generated plugins
Help(options.GenerateHelpText(env))
env.SConsignFile() # Single signature file

CLAMInstallDir = env['clam_prefix']
clam_sconstoolspath = os.path.join(CLAMInstallDir,'share','clam','sconstools')
if env['crossmingw'] :
	env.Tool('crossmingw', toolpath=[clam_sconstoolspath])
env.Tool('clam', toolpath=[clam_sconstoolspath])
env.moveIntermediateInto('generated')
env.activateColorCommandLine()
if not env['verbose']: env.ClamQuietCompilation()
env.EnableClamModules([
	'clam_core',
	'clam_audioio',
	'clam_processing',
	'clam_spacialization',
	] , CLAMInstallDir)

networks = glob.glob("*.clamnetwork")

env['LV2BASEURI'] = "http://clam-project.org/examples/lv2"
env['DOAPFILE'  ] = "default.doap"

manifest = env.Command("manifest.ttl", networks,
	"clam_lv2_generator.py --manifest -u ${LV2BASEURI} ${SOURCES} > ${TARGET}" )

mainSource = env.Command("clam_plugin.cxx", networks,
	"clam_lv2_generator.py --main -u ${LV2BASEURI} ${SOURCES} > ${TARGET}" )
ttls = [
	env.Command(os.path.splitext(os.path.basename(network))[0]+".ttl", network,
#	"clam_lv2_generator.py --ttl -u ${LV2BASEURI} ${SOURCE} > ${TARGET}" )
	"clam_lv2_generator.py --ttl -u ${LV2BASEURI} -d ${DOAPFILE} ${SOURCE} > ${TARGET}" )
	for network in networks ]


sources = Glob("*.cxx")

env.Append( CCFLAGS=['-g','-O3','-Wall'] )
libraries = [
	env.SharedLibrary(target=libraryName, source = sources, SHLIBPREFIX=''),
	]

install = env.Install(bundlePath, [libraries,ttls,manifest])
env.Alias('install', install)
env.Command('uninstall', [], Delete(FindInstalledFiles()))
env.Default(libraries, ttls, manifest)




