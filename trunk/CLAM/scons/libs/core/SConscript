# SConstruct file for CLAMcore
from buildtools import *
from buildtools.include_rename import update_includes
from buildtools.pkggen import PackageData
from buildtools.file_retriever import FileRetriever
from buildtools.rulesets import *

from buildtools.custom_builders import *
import os, sys

Import('core_env version top install_dirs header_db')

def define_module_contents() :
	if not os.path.exists('src') :
		os.mkdir('src')
	if not os.path.exists('include' ) :
		os.makedirs('include/CLAM')

	core_folders = ['src/Base', 'src/Data/Base', 'src/Data/BasicProcessing/Audio.*', 'src/Data/Descriptors/Pool', 'src/Defines', 'src/Errors', 'src/Flow/Ports', 'src/Flow/Networks', 'src/Flow/Controls', 'src/Processing/Base', 'src/Processing/Plugins','src/Standard', 'src/Storage', 'src/Storage/Base', 'src/Storage/XML','src/System','src/System/Threads', 'externals/CbLib']

	if sys.platform == 'win32' :
		core_folders.append('src/Defines/Windows')
	else :
		core_folders.append('src/Defines/Unix')	

	blacklist = ['ScaleCnv', 'LadspaBridge', 'BlockingNetworkPlayer', 'JACKNetworkPlayer', 'PANetworkPlayer']

	if sys.platform == 'win32' :
		blacklist.append( 'Watchdog.+' )

	if core_env['xmlbackend'] == 'xercesc' :
		blacklist.append( 'LibXml.+')
		blacklist.append( 'Null.+' )
	elif core_env['xmlbackend'] == 'xmlpp' :
		blacklist.append( 'Xerces.+')
		blacklist.append( 'Null.+' )
	elif core_env['xmlbackend'] == 'none' :
		blacklist.append( 'Xerces.+' )
		blacklist.append( 'LibXml.+' )

	if not core_env.has_key('with_ladspa_support') or core_env['with_ladspa_support'] == False :
		blacklist.append( 'Ladspa.+' )

	if not core_env.has_key('with_osc_support') or core_env['with_osc_support'] == False :
		blacklist.append( 'OSC.+')

	
	file_retriever = FileRetriever( top+'/..', core_folders, blacklist )

	file_retriever.scan_without_copy()

	builderCopy = Builder( action=Action(generate_copy_files,generate_copy_files_message) )
	core_env.Append( BUILDERS={'CopyFileAndUpdateIncludes' : builderCopy} )	

	realHeaders = []
	for orig,target in file_retriever.origTargetHeaders :
		core_env.CopyFileAndUpdateIncludes(target, orig)
		realHeaders.append(target)
	# preinclude is added not by #include but by compiler options
	#core_env.CopyFileAndUpdateIncludes('include/CLAM/preinclude.hxx', '../../../src/Defines/preinclude.hxx')
	#realHeaders.append('include/CLAM/preinclude.hxx')

	realSources = []
	for orig,target in file_retriever.origTargetSources:
		core_env.CopyFileAndUpdateIncludes(target,orig)
		realSources.append(target)

	manifest = Manifest()
	manifest.store( realHeaders, realSources )
	return realHeaders, realSources


if not core_env.GetOption('clean') :
	pkg_data = PackageData( 'clam_core', version )
	pkg_data.create_pkg_descriptor( core_env, 'clam_core.pc' )

realHeaders, realSources = define_module_contents()

core_env.Prepend(CPPPATH='include')

if sys.platform != 'win32' :
	core_tgt, core_install_tgt = posix_lib_rules( 'core', version, realHeaders , realSources, install_dirs, core_env )
else :
	# TODO whats about templates
	#templates = [ 'src/Point.cxx', 'src/Polar.cxx', 'src/Complex.cxx', 'src/BPF.cxx', 'src/Matrix.cxx', 'src/Order.cxx'] 
	core_tgt, core_install_tgt = win32_lib_rules( 'core', version, realHeaders, realSources, install_dirs, core_env )

Return( 'core_tgt','core_install_tgt' )
