import sys,os,glob

if sys.platform != 'darwin':
	print "ERROR: at this moment this example only compiles in OSX"
	sys.exit(1)

print """
#############################################################
WARNING: to compile this example properly first you must make
sure that the source files of this project have new-style headers!!!!"
 -This folders are to be converted in CLAM/examples:
	QtDesignerPlugins and Prototyper
#############################################################"""

paprot_env = Environment( tools=['default','qt']) 
paprot_env.Replace( QT_UIC='/Developer/qt/bin/uic' )
paprot_env.Replace(QT_LIB='qt-mt')

opts = Options('Annotator.conf')

opts.Add( PathOption( 'clam_prefix', 'Prefix were CLAM was installed', '/usr/local') )
opts.Add( BoolOption( 'release', 'Build CLAM [PortAudio] Prototyper enabling compiler optimizations', 'yes') )
opts.Update(paprot_env)
opts.Save('PAPrototyper.conf', paprot_env)
Help(opts.GenerateHelpText(paprot_env))

if paprot_env['release'] :
	paprot_env.Append( CPPFLAGS=['-O3','-fomit-frame-pointer','-Wall'] )
else :
	paprot_env.Append( CPPFLAGS=['-g', '-Wall'] )

export_pkg_config = 'export PKG_CONFIG_PATH='+paprot_env['clam_prefix']+'/lib/pkgconfig'
execute_pkg_config = 'pkg-config --cflags --libs clam_core clam_processing clam_audioio clam_vmqt' 
paprot_env.ParseConfig( export_pkg_config + ' && ' + execute_pkg_config  )

sourcefiles=['../../../examples/Prototyper/PAPrototyper.cxx',
	'../../../examples/Prototyper/PrototypeLoader.cxx',
	'../../../examples/Prototyper/QtSlot2Control.cxx',
	'../../../examples/QtDesignerPlugins/CLAMWidgetsPlugin.cxx']

bin_objects = [ paprot_env.Object(source) for source in sourcefiles ]

paprot_env.Append(CPPPATH='.')

#WARNING: the -DUSE_PORTAUDIO has been forced.
#The portaudio use intended in CLAM is as an audio backend, so it doesn't work properly
#BUT the use in this example is as a connectivity issue
paprot_env.Append(CPPFLAGS=['-include', 'CLAM/preinclude.hxx', '-DUSE_PORTAUDIO'])
paprot_env.Append(LINKFLAGS=['-dynamic','-bind_at_load'])
paprot_env.Append(LIBS=['qui'])

paprot_bin_pre = paprot_env.Program( 'PAPrototyper', bin_objects )

bundle_bin = paprot_env.Install( 'PAPrototyper.app/Contents/MacOS', paprot_bin_pre )
bundle_data = paprot_env.Install( 'PAPrototyper.app/Contents', 'mac_resources/Info.plist')
bundle_icon = paprot_env.Install( 'PAPrototyper.app/Contents/Resources', 'mac_resources/CLAM.icns')

paprot_bin = [ bundle_bin + bundle_data + bundle_icon ]
		
Default(paprot_bin)
