import sys,os,glob

if sys.platform != 'darwin':
	print "ERROR: at this moment this example only compiles in OSX"
	sys.exit(1)

print """
#############################################################
WARNING: to compile this example properly first you must make
sure that the source files of this project have new-style headers!!!!"
 -This folders are to be converted in CLAM/examples:
	QtDesignerPlugins and Prototyper
#############################################################"""

prot_env = Environment( tools=['default','qt']) 
prot_env.Replace( QT_UIC='/Developer/qt/bin/uic' )
prot_env.Replace(QT_LIB='qt-mt')

opts = Options('Annotator.conf')

opts.Add( PathOption( 'clam_prefix', 'Prefix were CLAM was installed', '/usr/local') )
opts.Add( BoolOption( 'release', 'Build CLAM [Blocking] Prototyper enabling compiler optimizations', 'yes') )
opts.Update(prot_env)
opts.Save('Prototyper.conf', prot_env)
Help(opts.GenerateHelpText(prot_env))

if prot_env['release'] :
	prot_env.Append( CPPFLAGS=['-O3','-fomit-frame-pointer','-Wall'] )
else :
	prot_env.Append( CPPFLAGS=['-g', '-Wall'] )

export_pkg_config = 'export PKG_CONFIG_PATH='+prot_env['clam_prefix']+'/lib/pkgconfig'
execute_pkg_config = 'pkg-config --cflags --libs clam_core clam_processing clam_audioio clam_vmqt' 
prot_env.ParseConfig( export_pkg_config + ' && ' + execute_pkg_config  )

sourcefiles=['../../../examples/Prototyper/BlockingPrototyper.cxx',
	'../../../examples/Prototyper/PrototypeLoader.cxx',
	'../../../examples/Prototyper/QtSlot2Control.cxx',
	'../../../examples/QtDesignerPlugins/CLAMWidgetsPlugin.cxx']

bin_objects = [ prot_env.Object(source) for source in sourcefiles ]

prot_env.Append(CPPPATH='.')

prot_env.Append(CPPFLAGS=['-include', 'CLAM/preinclude.hxx'])
prot_env.Append(LINKFLAGS=['-dynamic','-bind_at_load'])
prot_env.Append(LIBS=['qui'])

prot_bin_pre = prot_env.Program( 'Prototyper', bin_objects )

bundle_bin = prot_env.Install( 'Prototyper.app/Contents/MacOS', prot_bin_pre )
bundle_data = prot_env.Install( 'Prototyper.app/Contents', 'mac_resources/Info.plist')
bundle_icon = prot_env.Install( 'Prototyper.app/Contents/Resources', 'mac_resources/CLAM.icns')

prot_bin = [ bundle_bin + bundle_data + bundle_icon ]
		
Default(prot_bin)
