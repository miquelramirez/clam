#!/usr/bin/python
import os
import sys
import glob

options = Variables('options.cache', ARGUMENTS)



toolChain = 'default' if sys.platform is not 'win32' else 'mingw'
env = Environment(ENV=os.environ, tools=[toolChain], options=options)
options.Save('options.cache', env)
Help(options.GenerateHelpText(env))
env.SConsignFile() # Single signature file

env.ParseConfig('python-config --cflags')
env.ParseConfig('python-config --libs')
env.ParseConfig('pkg-config clam_core clam_processing clam_audioio --cflags --libs')

env.AppendUnique(
	LIBS=['boost_python']
)

# python-config adds a flag that is refused by g++
if '-Wstrict-prototypes' in env['CCFLAGS'] :
	env['CCFLAGS'].remove('-Wstrict-prototypes')

pythonc_module = env.SharedLibrary (
	target='Clam_NetworkProxy',
	source='Clam_NetworkProxy.cxx',
	SHLIBPREFIX='',
)

configurationModule = env.SharedLibrary (
	target='Clam_ConfigurationProxy',
	source=['Clam_ConfigurationProxy.cxx', 'ConfigurationProxy.cxx'],
	SHLIBPREFIX='',
)

dummyProcessingsModule = env.SharedLibrary (
	target='DummyProcessingsModule',
	source=['DummyProcessingsModule.cxx','DummyProcessings.cxx'],
	SHLIBPREFIX='',
)

program = env.Program(
	target='ipyclam_console',
	source='ipyclam_console.cxx',
)
blacklist = [
	"runTests.py",
	"Clam_ConfigurationProxyTests.py",
	"Clam_NetworkProxyTests.py",
	"ProxyAPI.py"
]

python_modules = [
	source for source in  glob.glob('*.py')
	if source not in blacklist
#	if "Test" not in source
	]

prefix = os.path.expanduser('~/local')
mod_dir = os.path.join(prefix, 'lib', 'python', 'ipyclam')
lib_dir = os.path.join(prefix, 'lib', 'python', 'ipyclam')
bin_dir = os.path.join(prefix, 'bin')

install = [
	env.Install(targetDir, files) for targetDir, files in [
	( lib_dir,[
		pythonc_module,
		python_modules,
		configurationModule,
		dummyProcessingsModule,
	]),
	( bin_dir,[
		program,
	]),
]]

env.Alias('install', install)

Default(
	pythonc_module,
	configurationModule,
	dummyProcessingsModule,
	program,
)
